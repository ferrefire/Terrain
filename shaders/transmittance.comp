#version 460

#extension GL_GOOGLE_include_directive : require

#include "atmosphere.glsl"

#include "variables.glsl"

layout(set = 1, binding = 0, rgba16f) uniform image2D transmittanceImage;
layout(set = 1, binding = 1, rgba16f) uniform readonly image2D scatteringImage;
layout(set = 1, binding = 2, rgba16f) uniform readonly image2D skyImage;
layout(set = 1, binding = 3, rgba16f) uniform readonly image3D aerialImage;

layout(local_size_x = 8, local_size_y = 4) in;

vec3 IntegrateTransmittance(vec3 worldPosition, vec3 worldDirection, uint sampleCount)
{
	float integrationLength = IntersectSphere(worldPosition, worldDirection, vec3(0.0), topRadius);
	float integrationStep = integrationLength / float(sampleCount);

	vec3 opticalDepth = vec3(0.0);

	for (int i = 0; i < sampleCount; i++)
	{
		vec3 rayPosition = worldPosition + i * integrationStep * worldDirection;
		vec3 atmosphereExtinction = SampleExtinction(rayPosition);
		opticalDepth += atmosphereExtinction * integrationStep;
	}

	return (opticalDepth);
}

void main()
{
	vec2 uv = gl_GlobalInvocationID.xy / transmittanceDimensions;
	vec2 uvT = UVToTransmittance(uv);

	vec3 worldPosition = vec3(0.0, uvT.x, 0.0);
	vec3 worldDirection = vec3(0.0, uvT.y, sqrt(1.0 - (uvT.y * uvT.y)));

	vec3 transmittance = exp(-IntegrateTransmittance(worldPosition, worldDirection, 400));

	imageStore(transmittanceImage, ivec2(gl_GlobalInvocationID.xy), vec4(transmittance, 1.0));
}